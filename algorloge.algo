#! /usr/bin/env fralgo

Importer "datelib" Alias dt
Importer "termlib" Alias term

Constante @ALGORLOGE_AUTEUR   "Stéphane MEYER (Teegre)"
Constante @ALGORLOGE_VERSION  "0.1"
Constante @ALGORLOGE_DATE_MOD "2025/04/01"

# Chiffres de l'horloge
Constante CHIFFRES [                         \\
  ["█████","██ ██","██ ██","██ ██","█████"], \\
  [" ███ ","  ██ ","  ██ ","  ██ ","  ██ "], \\
  ["█████","   ██","█████","██   ","█████"], \\
  ["█████","   ██","█████","   ██","█████"], \\
  ["██ ██","██ ██","█████","   ██","   ██"], \\
  ["█████","██   ","█████","   ██","█████"], \\
  ["█████","██   ","█████","██ ██","█████"], \\
  ["█████","   ██","   ██","   ██","   ██"], \\
  ["█████","██ ██","█████","██ ██","█████"], \\
  ["█████","██ ██","█████","   ██","█████"]  \\
]
# Couleurs de l'horloge.
Constante COULEURS [                 \\
  039, 038, 044, 043, 049, 048, 084, \\
  083, 119, 118, 154, 148, 184, 178, \\
  214, 208, 209, 203, 204, 198, 199, \\
  163, 164, 128, 129, 093, 099, 063, \\
  069, 033                           \\
]

# Largeur d'un chiffre (en caractères).
Constante LC 5
# Hauteur d'un chiffre (en caractères).
Constante HC 5
# Largeur totale de l'horloge.
Constante LH 6 * LC + 5
# Pause entre les affichages.
Constante PAUSE 0.5

# Barre de progression.
Constante TIC "█"

Structure TChiffre
  # Dizaines
  D en Entier
  # Unités
  U en Entier
FinStructure

Variables H,M,S en TChiffre
# Coordonnées de l'horloge.
Variables Y,X en Entier
# Forcer le réaffichage complet.
Variable FORCER en Booléen
# Suivre les changements de taille du terminal.
Variables lt, ht en Entier

Procédure Synchronise()
  Variable n en Entier
  Variable N en Chaîne
  
  n ← 1
  TantQue n <> 0
    N ← dt:Format(TempsUnix(), "%N")
    n ← Entier(Gauche(N, 1))
    Dormir(0.0625)
  FinTantQue

FinProcédure

Procédure CentrageHorloge()
  # Largeur et hauteur du terminal.
  Constante LT term:Largeur()
  Constante HT term:Hauteur()
  # Coordonnées du bord supérieur gauche.
  Y ← (HT - HC + 3) / 2
  X ← (LT - LH) / 2
  # Suivi d'éventuels changements de taile du terminal.
  Si NON(FORCER) Alors
    FORCER ← (LT <> lt) OU (HT <> ht)
  FinSi
  lt ← LT
  ht ← HT
FinProcédure

Procédure AfficheChiffre(x, c en Entier)
  # Affiche le chiffre 'c' donné.
  # 'x' est la colonne à laquelle doit débuter l'affichage.
  Variable y en Entier
  Variable couleur en Entier

  couleur ← COULEURS[Entier(Aléa() * 30)]

  Pour y ← Y à Y + HC - 1
    term:PosEcrire(y, x, term:CT(couleur) & CHIFFRES[c, y - Y])
  y Suivant

FinProcédure

Procédure AfficheBarre(sbp en Entier)
  # Teegre :
  # Affiche une barre de progression
  # selon un nombre de secondes 'sbp' donné.
  Variables C, y, x en Entier
  Variable couleur en Chaîne

  Si sbp = 0 Alors
    EffacerBarre()
    Terminer
  FinSi
  
  C ← COULEURS[Entier((sbp * LH / 60) % 29)]
  couleur ← term:CT(C)
  y ← Y - 2
  x ← X + Entier(sbp * LH / 60)
  term:PosEcrire(y, x, couleur & TIC & term:ANN)
  term:EffaceFL()

FinProcédure

Procédure RéafficheBarre(sbp en Entier)
  # Affiche graduellement la barre de progression
  # jusqu'au nombre de secondes donné.
  Variable i en Entier
  Pour i ← 0 à sbp
    AfficheBarre(i)
    Dormir(0.0078125)
  i Suivant
FinProcédure

Procédure EffacerBarre()
  # Efface graduellement la barre de progression.
  Variable i en Entier
  Variables y, x en Entier
  y ← Y - 2
  x ← X
  Pour i ← LH à 1 Pas -1
    term:PosEcrire(y, x + i, " ")
    Dormir(0.0078125)
  i Suivant
FinProcédure

Procédure AfficheHorloge()
  # Affiche l'horloge.
  # N'affiche que ce qui est nécessaire, c.à.d. :
  # pas de réaffichage complet de l'horloge à chaque appel de la procédure.
  Variables date, DATE, HEURE en Chaîne
  Variables h, m, s en TChiffre
  # Secondes pour la barre de progression.
  Variable sbp en Entier
  # Coordonnées.
  Variables y, x en Entier
  
  # Date courante du système (ex. lun. 31/03)
  date ← std:Majus(dt:Format(TempsUnix(), "%a %d/%m"))
  # Date formatée (ex. LU 31/03)
  DATE ← Extraire(date, 1, 2) & Droite(date, 6)
  # Heure courante du système.
  HEURE ← dt:Format(TempsUnix(), "%H%M%S")

  CentrageHorloge()

  #   Dizaines                       Unités
  h ← Entier(Extraire(HEURE, 1, 1)), Entier(Extraire(HEURE, 2, 1))
  m ← Entier(Extraire(HEURE, 3, 1)), Entier(Extraire(HEURE, 4, 1))
  s ← Entier(Extraire(HEURE, 5, 1)), Entier(Extraire(HEURE, 6, 1))

  sbp ← Entier(Extraire(HEURE, 5, 2))

  Si FORCER Alors
    term:EffaceEcran()
    RéafficheBarre(sbp)
  FinSi
  
  # Heures
  ## Dizaines
  Si h.D <> H.D OU FORCER Alors
    x ← X
    AfficheChiffre(x, h.D)
    H.D ← h.D
    term:PosEcrire(Y+HC, x - 8 + LH, DATE)
  FinSi
  ## Unités
  Si h.U <> H.U OU FORCER Alors
    x ← X + LC + 1
    AfficheChiffre(x, h.U)
    H.U ← h.U
  FinSi
  # Minutes
  ## Dizaines
  Si m.D <> M.D OU FORCER Alors
    x ← X + 2 * LC + 2
    AfficheChiffre(x, m.D)
    M.D ← m.D
  FinSi
  ## Unités
  Si m.U <> M.U OU FORCER Alors
    x ← X + 3 * LC + 3
    AfficheChiffre(x, m.U)
    M.U ← m.U
  FinSi
  # Secondes
  ## Dizaines
  Si s.D <> S.D OU FORCER Alors
    x ← X + 4 * LC + 4
    AfficheChiffre(x, s.D)
    S.D ← s.D
  FinSi
  ## Unités
  Si s.U <> S.U OU FORCER Alors
    x ← X + 5 * LC + 5
    AfficheChiffre(x, s.U)
    AfficheBarre(sbp)
    S.U ← s.U
  FinSi

  FORCER ← FAUX

FinProcédure

Début
  term:SauveEcran()
  term:CacheCur()

  lt ← term:Largeur()
  ht ← term:Hauteur()

  H ← -1,-1
  M ← -1,-1
  S ← -1,-1

  FORCER ← VRAI

  TantQue VRAI
    Synchronise()
    AfficheHorloge()
    Dormir(PAUSE)
  FinTantQue
Fin
